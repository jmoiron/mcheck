package main

type MCDocParser Peg {
}

Start <- _ Statement* _ !.

Statement <- (Attribute* _ (
	UseStmt /
	TypeAlias /
	StructDef /
	EnumDef /
	DispatchStmt
)) _

UseStmt <- 'use' _ Path
Path <- DoubleColon? PathSegment (DoubleColon PathSegment)*
PathSegment <- ('super' / Identifier)

TypeAlias <- 'type' _ TypeName _ EQUALS Type
TypeName <- (GenericType / Identifier)

StructDef <- 'struct' _ Identifier _ LBRACE FieldList? RBRACE
FieldList <- FieldOrSpread (COMMA FieldOrSpread)* COMMA?
FieldOrSpread <- SpreadField / Field
Field <- Attribute* _ (ComputedField / NamedField)
ComputedField <- LBRACKET Type RBRACKET QUESTION? COLON Type
NamedField <- FieldName COLON Type
SpreadField <- Attribute* _ SPREAD Type
FieldName <- Identifier QUESTION?

EnumDef <- 'enum' _ LPAREN Type RPAREN Identifier _ LBRACE EnumValueList? RBRACE
EnumValueList <- EnumValue (COMMA EnumValue)* COMMA?
EnumValue <- Attribute* _ Identifier _ EQUALS String

DispatchStmt <- 'dispatch' _ DispatchPath _ 'to' _ DispatchTarget
DispatchPath <- Identifier COLON ResourcePath LBRACKET DispatchKeyList RBRACKET (LT GenericTypeParams RT)?
DispatchKeyList <- DispatchKey (COMMA DispatchKey)* COMMA?
DispatchKey <- (StaticIndexKey / String / Identifier)
DispatchTarget <- ('struct' _ Identifier _ LBRACE FieldList? RBRACE) / Type

SpreadStruct <- SPREAD 'struct' _ Identifier _ LBRACE FieldList? RBRACE

Type <- (
	UnionType /
	AttributedType /
	ArrayType /
	StructType /
	ConstrainedType /
	GenericType /
	PrimitiveType /
	ReferenceType /
	LiteralType
)

AttributedType <- Attribute+ _ (UnionType / ArrayType / ConstrainedType / StructType / GenericType / PrimitiveType / ReferenceType / LiteralType)

ConstrainedType <- (PrimitiveType / ReferenceType / LiteralType) ArrayConstraint

UnionType <- LPAREN Type (PIPE Type)* PIPE? RPAREN
ArrayType <- (LBRACKET Type RBRACKET ArrayConstraint?) / (PrimitiveType LBRACKET RBRACKET) / (ReferenceType LBRACKET RBRACKET)
StructType <- 'struct' _ Identifier? _ LBRACE FieldList? RBRACE
GenericType <- Identifier LT GenericTypeParams RT
GenericTypeParams <- Type (COMMA Type)*
PrimitiveType <- ('string' / 'double' / 'float' / 'int' / 'boolean' / 'any') _
ReferenceType <- (ComplexReference / Path / Identifier)
ComplexReference <- Identifier COLON ResourcePath (LBRACKET LBRACKET ComplexRefParam RBRACKET RBRACKET / LBRACKET ComplexRefParam RBRACKET) (LT GenericTypeParams RT)?
ResourcePath <- Identifier ('/' Identifier)*
ComplexRefParam <- (DottedPath / StaticIndexKey / String / Identifier)
DottedPath <- (StaticIndexKey / Identifier) ('.' Identifier)+
StaticIndexKey <- ('%fallback' / '%key' / '%parent' / '%none' / '%unknown') _
LiteralType <- (String / Number / Boolean)

ArrayConstraint <- AT (Range / Number)
Range <- (Number RangeOperator Number) / (Number RangeOperator) / (RangeOperator Number)
RangeOperator <- LT? DOTDOT LT?

Attribute <- '#' LBRACKET AttributeList RBRACKET
AttributeList <- AttributeItem (COMMA AttributeItem)*
AttributeItem <- (AttributePair / AttributeCall / AttributeCallWithEquals / Identifier)
AttributeCallWithEquals <- Identifier EQUALS LPAREN AttributeParamList? RPAREN
AttributeCall <- Identifier LPAREN AttributeParamList? RPAREN
AttributeParamList <- AttributeParam (COMMA AttributeParam)*
AttributeParam <- (AttributePair / AttributeValue)
AttributePair <- Identifier EQUALS AttributeValue
AttributeValue <- (ArrayLiteral / ComplexReference / String / Number / Boolean / Identifier)

ArrayLiteral <- LBRACKET (AttributeValue (COMMA AttributeValue)*)? RBRACKET

Comment <- '//' (!EOL .)* (EOL / !.)
DocComment <- '///' (!EOL .)* (EOL / !.)

Identifier <- [a-zA-Z_][a-zA-Z0-9_]* _
String <- '"' (!'"' .)* '"' _
Number <- '-'? [0-9]+ ('.' [0-9]+)? _
Boolean <- ('true' / 'false') _

# Separator tokens with optional trailing whitespace
LBRACE <- '{' _
RBRACE <- '}' _
LBRACKET <- '[' _
RBRACKET <- ']' _
LPAREN <- '(' _
RPAREN <- ')' _
COMMA <- ',' _
COLON <- ':' _
SEMICOLON <- ';' _
EQUALS <- '=' _
PIPE <- '|' _
DOT <- '.' _
SPREAD <- '...' _
AT <- '@' _
LT <- '<' _
RT <- '>' _
DOTDOT <- '..' _
QUESTION <- '?' _

DoubleColon <- '::' _
SingleColon <- ':' _

_ <- ([ \t\r\n] / Comment / DocComment)*
EOL <- '\r\n' / '\n' / '\r'